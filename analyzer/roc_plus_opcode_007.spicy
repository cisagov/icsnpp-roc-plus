module ROC_PLUS_OPCODE_007;

import spicy;
import ROC_PLUS_ENUMS;
import ROC_PLUS_FUNCTIONS;
import ROC_PLUS_GLOBALS;

type ReadRealtimeClock = unit {
    switch(ROC_PLUS_GLOBALS::PACKET_TYPE_GLOBAL) {
        ROC_PLUS_ENUMS::PacketType::REQUEST  -> request  : ReadRealtimeClockRequest();
        ROC_PLUS_ENUMS::PacketType::RESPONSE -> response : ReadRealtimeClockResponse();
    };
};

type ReadRealtimeClockRequest = unit {
    # ReadRealtimeClock request does not contain any data
};

type ReadRealtimeClockResponse = unit {
    var timestamp : time;

    currentSecond    : uint8;
    currentMinute    : uint8;
    currentHour      : uint8;
    currentDay       : uint8;
    currentMonth     : uint8;
    currentYear      : uint16;
    currentDayOfWeek : uint8 &convert=ROC_PLUS_ENUMS::DayOfWeek($$);

    on %done {
        self.timestamp = spicy::mktime(self.currentYear, self.currentMonth, self.currentDay, self.currentHour, self.currentMinute, self.currentSecond);
    }
};